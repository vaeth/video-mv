#compdef dvb-t dvb-c
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
local args dvbt dvbc
case $service in
(*t)
	capletter=T
	dvbt=:
	dvbc=false;;
(*)
	capletter=C
	dvbt=false
	dvbc=:;;
esac
args=(
'(1 2 -)'{'-h','-H'}'[help]'
'(1 -p)-r[set up /dev/dvb/adapterX/dvr0]'
'(1 -r)-p[add pat and pmt, implies -r]'
'-n[dry run\: execute nothing]'
"-M[-C 'mediaclient --setdtvmode=DVB$capletter']"
'-L[Do not extend LD_PRELOAD]'
'*-C+[Eval specified command in advance]:command:()'
'*-c+[Set specified channel file]:channel file:_files'
'*-T+[set specified title option]:title option:_title_opt'
'-a+[set specified adapter]:adapter:()'
'-F+[set specified frontend (numeric)]:frontend:()'
'-d+[set specified demux (numeric)]:demux:()'
'-s+[set specified start time)]:start time:("23\:59" "23\:59\:00 31 Dec 2013" "23\:59\:01 today" "now")'
{'(-e)-t','(-t)-e'}'+[set specified recording duration]:stop time:->stoptime'
'-q[quiet]'
'1:channel:->channel'
'2:output file:_files'
)
_arguments -s -S -A '-*' : $args
local ret=$?
case $state in
(channel)
	local chf channel flist
	chf=$(emulate sh && {
		dvb=:
		if test -r /etc/videodefaults
		then	. /etc/videodefaults
		else	. videodefaults
		fi
	} >/dev/null 2>&1 && printf '%s' "${channels}x") || chf=
	chf=${chf%x}
	flist=($chf)
	if $dvbt
	then	flist+=("$HOME/.tzap/channels.conf" \
			/etc/channels-tzap.conf)
	else	flist+=("$HOME/.czap/channels.conf" \
			/etc/channels-czap.conf)
	fi
	flist+=(/etc/channels.conf)
	channel=(ARD BBC CNN)
	for chf in $flist
	do	if [[ -n $chf && -r $chf ]]
		then	channel=(${(f)"$(sed -n -e '/^#/d' -e 's/:.*//p' -- "$chf" 2>/dev/null)"})
			break
		fi
	done
	channel+=(-)
	_describe -t channel 'DVB-T channel' channel && ret=0;;
(stoptime)
	local stoptime
	stoptime=(
	'01\:00\:00:hh\:mm:\:ss'
	'3600:seconds'
	)
	_describe -t time 'stop time' stoptime && ret=0;;
esac
return ret
