#compdef dvb-t
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
_arguments -s -S -A '-?*' : \
'(1 2 -)'{'-h','-H','-\\?'}'[help]' \
'(1 -p)-r[set up /dev/dvb/adapterX/dvr0]' \
'(1 -r)-p[add pat and pmt, implies -r]' \
'-q[quiet]' \
'*-c+[FILE (channel file)]:channel file:_files' \
'-s+[TIME (set start)]:start time:("23\:59" "23\:59\:00 31 Dec 2013" "23\:59\:01 today" "now")' \
{'(-e)-t','(-t)-e'}'+[TIME (set stop/length)]:stop time:->stoptime' \
'*-T+[OPT (set title option)]:title option:_title_opt' \
'-a+[NUM (set adapter)]:adaptor:()' \
'-F+[NUM (set frontend)]:frontend:()' \
'-d+[NUM (set demux)]:demux:()' \
'1:channel:->channel' \
'2:output file or -:_files'
local ret=$?
case $state in
(channel)
	local chf channel
	chf=$(emulate sh && {
		dvb=:
		if test -r /etc/videodefaults
		then	. /etc/videodefaults
		else	. videodefaults
		fi
	} >/dev/null 2>&1 && printf '%s' "${channels}x") || chf=
	chf=${chf%x}
	: ${chf:=${HOME}/.tzap/channels.conf}
	if [[ -r $chf ]] || {
		chf='/etc/channels.conf'
		[[ -r $chf ]]
	}
	then	channel=(${(f)"$(sed -n -e 's/:.*//p' -- "$chf" 2>/dev/null)"})
	else	channel=(ARD BBC CNN)
	fi
	_describe -t channel 'DVB-T channel' channel
	ret=$?;;
(stoptime)
	local stoptime
	stoptime=(
	'01\:00\:00:hh\:mm:\:ss'
	'3600:seconds'
	)
	_describe -t time 'stop time' stoptime
	ret=$?;;
esac
return ret
